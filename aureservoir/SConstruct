#####################################################################
#
#  ::::_aureservoir_::::
#  C++ library for analog reservoir computing neural networks
#
#  Scons build system for aureservoir
#
#  Georg Holzmann, 2007
#
#  This library is free software; you can redistribute it and/or
#  modify it under the terms of the GNU Lesser General Public
#  License as published by the Free Software Foundation; either
#  version 2.1 of the License, or (at your option) any later version.
#
#####################################################################

import os, glob, sys, os.path

#####################################################################
#  build files
#####################################################################

libname = "aureservoir"
sources = glob.glob("*.cpp")   # compile all *.cpp files

#####################################################################
#  build system help
#####################################################################

Help("\nType: 'scons' to build aureservoir library.")
Help("\n      'scons -c' to clean objects and library.\n")

#####################################################################
#  general build options
#####################################################################

env = Environment(ENV = {'PATH' : os.environ['PATH'],
                         'TERM' : os.environ['TERM'],
                         'HOME' : os.environ['HOME']})
env.Append( CPPPATH=['.',] )
env.Append(CCFLAGS="-O2 -fPIC -Wall -ffast-math -mfpmath=sse -msse -msse2")
# TODO: make also extra debug and release build !

#####################################################################
#  command line options
#####################################################################

opt = Options('options.cache')
opt.AddOptions(
  PathOption('flens_path', 'include path for FLENS', None),
  ('prefix', 'install prefix', '/usr/local'),
  ('arch', 'optimize for specific architecture (e.g. pentium4)', None)
)
opt.Update(env)
opt.Save('options.cache',env)
Help(opt.GenerateHelpText(env))

if env.has_key('flens_path'):
	env.Append(CPPPATH=[env['flens_path']])
if env.has_key('prefix'):
	prefix = env['prefix']
if env.has_key('arch'):
	env.Append(CCFLAGS="-march=" + env['arch'])

#####################################################################
#  check dependencies
#####################################################################

# don't check in help or clean mode
checking = 1
if "-h" in sys.argv:
	checking = 0
if env.GetOption('clean'):
	checking = 0

# do the checks
conf = Configure(env)
if checking:
	if not conf.CheckLib('flens', language="C++"):
		print 'Did not find FLENS library !'
		Exit(1)
	if not conf.CheckCXXHeader('flens/flens.h'):
		print 'Did not find FLENS header (flens/flens.h) !'
		Exit(1)
	if not conf.CheckHeader('xmmintrin.h'):
		print 'Did not find xmmintrin.h !'
		Exit(1)
env = conf.Finish()

#####################################################################
#  build library
#####################################################################

aureservoir = env.SharedLibrary(libname, sources)
#aureservoir = env.Program(libname, sources)
env.Default(aureservoir)

#####################################################################
#  install library
#####################################################################

headers = [ 'aureservoir.h',
            'auexcept.h',
            'utilities.h',
            'esn.h' ]

libinstall = env.Install(os.path.join(env['prefix'], "lib"),
                      aureservoir)
headerinstall = env.Install(os.path.join(env['prefix'], "include",
                            "aureservoir"), headers)

env.Alias("install", [libinstall, headerinstall])

#####################################################################
#  EOF
#####################################################################
